/*
Author: Nikki Pelot, adopted for SPARC ASCENT Pipeline by Eric Musselman

Description:

Instructions:
- Open NEURON's rxvt sh
- mpiexec -n <# processors> nrniv -mpi <.hoc filename including ".hoc" file extension>
*/

// Create parallel context instance
objref pc
pc = new ParallelContext()
print "number of processors: ", pc.nhost

if (!((passive_end_nodes == 0) || (passive_end_nodes == 1))) {
	print "passive_end_nodes must be set to 0 or 1"
	execerror("passive_end_nodes must be set to 0 or 1")
}
if (passive_end_nodes==0) {
	print "WARNING - Even though passive_end_nodes was set to 0, all fibers built with cFiberBuilder.hoc (fiber_type = 3, all c_fiber_model_type values) will have passive end nodes..."
}

// ***************************************************************************
// CreateFiber_Unmyel.hoc (psuedo c-fiber -- you might actually be interestied in cFiberBuilder.hoc)
if (fiber_type == 1) {
	load_file("CreateFiber_Unmyel.hoc")
	Createfiber_Unmyel()
}

// ***************************************************************************
// CreateFiber_Myel.hoc	
if (fiber_type == 2) {
	load_file("CreateFiber_Myel.hoc")
	CreateFiber_Myel()
}

// ***************************************************************************
// cFiberBuilder.hoc
load_file("cFiberBuilder.hoc")
if (fiber_type == 3) {
	load_file("CreateFiber_CFiber.hoc")
	CreateFiber_CFiber() 
}

	   if 	(fiber_type == 1) { 	v_init = -88.3 				// [mV]
} else if   (fiber_type == 2) { 	v_init = -80 				// [mV]
} else if 	(fiber_type == 3) {		v_init = v_init_c_fiber		// [mV]
}

// IntracellularStim.hoc
load_file("IntracellularStim.hoc")
IntraStim_PulseTrain()

// ***************************************************************************
// ExtracellularStim_Time.hoc
load_file("ExtracellularStim_Time.hoc")
if (flag_extracellular_stim == 1) {
	VeTime_read()
}

// ***************************************************************************
// ExtracellularStim_Space.hoc
strdef VeSpace_fname
load_file("ExtracellularStim_Space.hoc")

// ***************************************************************************
// Recording.hoc
objref checknode_values
checknode_values = new Vector(Nchecknodes,0)

if (Nchecknodes == fibernodes) {
	for i = 0, fibernodes - 1 {
		checknode_values.x[i] = i
	}
} else {
	if (fiber_type == 2) { // MRG
		fiber_length = (fibernodes-1)*deltaz				// length of fiber [um]
		checknode_values.x[0] = int(1000/deltaz)
		checknode_values.x[1] = int(0.5*(fibernodes-1))
		checknode_values.x[2] = int((fiber_length-1000)/deltaz)
	}

	if (fiber_type == 3) { // CFiber
		fiber_length = (fibernodes-1)*deltaz				// length of fiber [um]
		checknode_values.x[0] = int(0.1*(fibernodes-1))
		checknode_values.x[1] = int(0.5*(fibernodes-1))
		checknode_values.x[2] = int(0.9*(fibernodes-1))
	}
}

load_file("Recording.hoc")
Recording()

// ***************************************************************************
// Call sequence of procedures/functions for each sim
load_file("RunSim.hoc")

strdef fname_output_Vm_time
strdef fname_output_Vm_space
strdef fname_output_gating_m_time, fname_output_gating_h_time, fname_output_gating_mp_time, fname_output_gating_s_time
strdef fname_output_gating_m_space, fname_output_gating_h_space, fname_output_gating_mp_space, fname_output_gating_s_space
strdef Ve_fname_output, Istim_fname_output
strdef runtime_fname_output
strdef thresh_fname_output

load_file("Saving.hoc")
load_file("Saving_Runtime.hoc")

if (find_thresh == 1){
	load_file("FindThresh.hoc")
	load_file("Saving_Thresh.hoc")
}

proc run_all(){local myinner, myfiber, myfreq, myamp, mypw, myisi
	myinner  = $1
	myfiber  = $2
	myfreq  = $3
	myamp   = $4
	mypw    = $5
	myisi   = $6
	
	// Set PW of intracellular stim
	stim1.PW = mypw
	stim2.PW = mypw
	
	// Set ISI between intracellular stim and duration of second stim train
	stim2.del =  stim1.del + myisi
	stim2.train = tstop - stim2.del
	trun_individual = startsw()
	
	// Read in Ve(x)
	sprint(VeSpace_fname, "data/inputs/inner%d_fiber%d.dat",	myinner,myfiber)
	VeSpace_read()
	// Convert to mV (required for e_extracellular)
	VeSpace_data = VeSpace_data.mul(1000)
	
	if (find_thresh == 0) {
		RunSim(myamp)
		
		// Save data
		sprint(Ve_fname_output, 			 "data/outputs/Ve_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat", 			   myinner, myfiber, myamp, myfreq)
		sprint(Istim_fname_output,   		 "data/outputs/Istim_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat", 		   myinner, myfiber, myamp, myfreq)
		sprint(fname_output_Vm_time, 		 "data/outputs/Vm_time_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat", 		   myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_m_time,   "data/outputs/gating_m_time_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",   myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_h_time,   "data/outputs/gating_h_time_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",   myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_mp_time,  "data/outputs/gating_mp_time_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",  myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_s_time,   "data/outputs/gating_s_time_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",   myinner, myfiber, myamp, myfreq)
		sprint(fname_output_Vm_space, 		 "data/outputs/Vm_space_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",        myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_m_space,  "data/outputs/gating_m_space_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",  myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_h_space,  "data/outputs/gating_h_space_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",  myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_mp_space, "data/outputs/gating_mp_space_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat", myinner, myfiber, myamp, myfreq)
		sprint(fname_output_gating_s_space,  "data/outputs/gating_s_space_inner%d_fiber%d_Vblocamp%fmA%fkHz.dat",  myinner, myfiber, myamp, myfreq)
		Saving()
		
		// Save individual run time (for each simulation)
		trun_individual = startsw() - trun_individual
		sprint(runtime_fname_output, "data/outputs/runtime_inner%d_fiber%d_Vblocamp%f.dat", myinner, myfiber, myamp)
		Saving_Runtime()

	}   else if (find_thresh == 1){
		FindThresh(myinner)
		RunSim(stimamp)

		// Save data			
		sprint(Ve_fname_output,              "data/outputs/Ve_inner%d_fiber%d.dat",              myinner, myfiber)
		sprint(Istim_fname_output,           "data/outputs/Istim_inner%d_fiber%d.dat",           myinner, myfiber)
		sprint(fname_output_Vm_time,         "data/outputs/Vm_time_inner%d_fiber%d.dat",         myinner, myfiber)
		sprint(fname_output_gating_m_time,   "data/outputs/gating_m_time_inner%d_fiber%d.dat",   myinner, myfiber)
		sprint(fname_output_gating_h_time,   "data/outputs/gating_h_time_inner%d_fiber%d.dat",   myinner, myfiber)
		sprint(fname_output_gating_mp_time,  "data/outputs/gating_mp_time_inner%d_fiber%d.dat",  myinner, myfiber)
		sprint(fname_output_gating_s_time,   "data/outputs/gating_s_time_inner%d_fiber%d.dat",   myinner, myfiber)
		sprint(fname_output_Vm_space,        "data/outputs/Vm_space_inner%d_fiber%d.dat",        myinner, myfiber)
		sprint(fname_output_gating_m_space,  "data/outputs/gating_m_space_inner%d_fiber%d.dat",  myinner, myfiber)
		sprint(fname_output_gating_h_space,  "data/outputs/gating_h_space_inner%d_fiber%d.dat",  myinner, myfiber)
		sprint(fname_output_gating_mp_space, "data/outputs/gating_mp_space_inner%d_fiber%d.dat", myinner, myfiber)
		sprint(fname_output_gating_s_space,  "data/outputs/gating_s_space_inner%d_fiber%d.dat",  myinner, myfiber)
		
		Saving()
		
		// Save individual run time (for each simulation)
		trun_individual = startsw() - trun_individual
		sprint(runtime_fname_output, "data/outputs/runtime_inner%d_fiber%d.dat", myinner, myfiber)
		Saving_Runtime()
		
		// Save threshold value
		sprint(thresh_fname_output, "data/outputs/thresh_inner%d_fiber%d.dat", myinner, myfiber)
		Saving_Thresh()
	} 
}

// ***************************************************************************
// Loop through model numbers, innericles (different Ve(x) profiles), Ve amplitudes, Ve freq's, fibers (multiple within 1 innericle)

objref inner_values
inner_values = new Vector(Ninners,0)
for inner_ind = 0, Ninners-1 {
	inner_values.x[inner_ind] = inner_ind
}

objref num_fibers_file, fiber_values
num_fibers_file = new File()
num_fibers_file.ropen(num_fibers_fname)
fiber_values = new Vector(Ninners)
fiber_values.scanf(num_fibers_file)
num_fibers_file.close()

trun = startsw()
// Start execute loop on the workers
{pc.runworker()}
// Code beyond this point (til pc.done()) is only executed by the master.
// The master must now post jobs to the bulletin board.

proc batchrun() {local inner_ind, fiber_ind, Vefreq_ind, stimamp_ind, pw_ind, isi_ind
	for inner_ind = 0, Ninners-1 {
		for fiber_ind = 0, fiber_values.x[inner_ind]-1 {
			for Vefreq_ind = 0, Nfreq-1 {
				for stimamp_ind = 0, Namp-1 {
					for pw_ind = 0, Npw-1 {
						for isi_ind = 0, Nisi-1 {
							// myinner, myfiber, myfreq, myamp, mypw, myisi
							pc.submit("run_all",inner_values.x[inner_ind],fiber_ind,Vefreq_values.x[Vefreq_ind],stimamp_values.x[stimamp_ind],pw_values.x[pw_ind],isi_values.x[isi_ind])
						}
					}
				}
			}
		}
	}
	// If a result is ready, get it. If not, pick a job to do.
	while (pc.working()) {
	}
}
batchrun()

// Save the runtime to a file
objref f_runtime
f_runtime = new File()
f_runtime.wopen("data/outputs/run_time.dat")
f_runtime.printf("Run time: %f seconds", startsw()-trun)
f_runtime.close()
{pc.done()}
//quit()
