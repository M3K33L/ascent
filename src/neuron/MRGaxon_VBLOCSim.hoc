/*--------------------------------------------------------------------
2/02
Cameron C. McIntyre
SIMULATION OF PNS MYELINATED AXON

This model is described in detail in:

McIntyre CC, Richardson AG, and Grill WM. Modeling the excitability of
mammalian nerve fibers: influence of afterpotentials on the recovery
cycle. Journal of Neurophysiology 87:995-1006, 2002.

This model can not be used with NEURON v5.1 as errors in the
extracellular mechanism of v5.1 exist related to xc. The original
stimulations were run on v4.3.1. NEURON v5.2 has corrected the 
limitations in v5.1 and can be used to run this model.

11/12
Modified by N. Pelot to simulate VBLOC
----------------------------------------------------------------------*/
/*
To change fiber diameter, change:
- fiberD
- axonnodes
- filename in Ve_read()
- filename in save_data()
*/


//load_proc("nrnmainmenu")

// Create parallel context instance
objref pc
pc = new ParallelContext()

print "number of processors: ", pc.nhost

// ******************************************* Define parameters *************************************************
proc model_globals() {
	celsius		= 37						// [degC]
	v_init		= -80 						// [mV]
	dt			= 0.005 					// [ms]
	tstop 		= 100						// [ms]
	n_tsteps 	= int(tstop/dt) + 1			// int truncates twds zero; +1 accounts for the time point at t=0 [unitless]

//Start before t=0 with large fixed timestep to allow the system to reach SS
	t_initSS	= -200						// [ms]
	dt_initSS	= 10						// [ms]
	
//Topological parameters [unitless]//		
	axonnodes	= 201
	paranodes1  = 2*(axonnodes-1)  		// MYSA paranodes
	paranodes2  = 2*(axonnodes-1)  		// FLUT paranodes
	axoninter   = 6*(axonnodes-1)  		// STIN internodes
	axontotal 	= axonnodes + paranodes1 + paranodes2 + axoninter

	/*
	paranodes1=40
	paranodes2=40	
	axoninter=120			
	axontotal=221			 
	*/

//Morphological parameters//	
	fiberD		= 5.7							// choose from 1, 2, 5.7, 7.3, 8.7, 10.0, 11.5, 12.8, 14.0, 15.0, 16.0; [um]
	// *** CHANGE file name in Ve_read() as well to match fiberD
	nodelength	= 1.0						// [um]
	paralength1	= 3  						// [um]
	space_p1	= 0.002						// [??]
	space_p2	= 0.004						// [??]
	space_i		= 0.004						// [??]


//Intracellular test stimulus parameters//
	delay				= 25							// [ms]
	pw					= 0.1						// [ms]
	traindur1			= tstop-delay				// [ms]
	ifreq				= 100						// [Hz]
	istim				= 2						// [nA]
	//x_intracellstim_ind = int(axonnodes/10)			// [unitless]
	
//VBLOC stimulation parameters//
	Nvblocamp 			= 1							// number of stimulation amplitudes to try
	Nvblocfreq 			= 4							// number of stimulation frequencies to try
	
	// VBLOC
	vblocdelay1			= 1							// [ms]
	//vblocpw1			= 0.1						// [ms]
	vblocdur1			= tstop - vblocdelay1		// [ms]
	//vblocfreq1			= 5000						// [Hz]
	vblocistim1			= -1						//
	
	//vblocdelay2			= vblocdelay1 + vblocpw1	// [ms]	
	//vblocpw2			= vblocpw1					// [ms]
	//vblocdur2			= tstop - vblocdelay2		// [ms]
	//vblocfreq2			= vblocfreq1				// [Hz]
	vblocistim2			= -1 * vblocistim1			//

	// nodes where Vm is recorded [unitless]
	Nchecknodes 		= 4						// number of nodes at which Vm(t) is recorded
	
	/*
	// node where the extracellular stimulation (Ve) is recorded 
	checknode1			= int(axonnodes/2)
	
	// nodes where Vm is recorded [unitless]
	checknode2 			= int(0.25*axonnodes)
	checknode3 			= int(0.5*axonnodes)
	checknode4 			= int(0.75*axonnodes)
	*/
	
	// Model numbers are coded in modelnumbers.xlsx.
	Nmodel				= 1							// number of different models (meshes, element order, length and radius of the medium, etc. in COMSOL)
	Nfasc				= 1							// number of fascicles within axons being simulated
	Naxon				= 1							// number of axons per fascicle being simulated
	
//Electrical parameters//		
	rhoa	= 0.7e6 						// [ohm-um]
	mycm	= 0.1 							// lamella membrane; [uF/cm2]
	mygm	= 0.001 						// lamella membrane; [S/cm2]
	rhoe	= 1000  						// resistivity of extracellular medium; [ohm-cm]
	
	}
model_globals ()

// Define models, fascicles, and axons to try
objref model_values, fasc_values, axon_values
model_values = new Vector(Nmodel,0)
fasc_values = new Vector(Nfasc,0)
axon_values = new Vector(Naxon,0)

model_values.x[0] = 119

/*
for K = 0, Nfasc-1 {
	fasc_values.x[K] = K+1
}
*/
fasc_values.x[0] = 7

axon_values.x[0] = 1

// Define vector of VBLOC amplitudes to try [mA]
objref vblocamp_values
vblocamp_values = new Vector(Nvblocamp,0)
/*
for K = 0, Nvblocamp-1 {
	vblocamp_values.x[K] = K
}
*/
vblocamp_values.x[0] = 1

// Define vector of VBLOC frequencies to try [Hz]
objref vblocfreq_values
vblocfreq_values = new Vector(Nvblocfreq,0)
vblocfreq_values.x[0] = 5000
vblocfreq_values.x[1] = 10000
vblocfreq_values.x[2] = 15000
vblocfreq_values.x[3] = 20000

proc dependent_var() {
	// [um]
	if (fiberD==1) 			{axonD=0.8 nodeD=0.7 paraD1=0.7 paraD2=0.8 deltax=100 paralength2=5 nl=15}
	if (fiberD==2) 			{axonD=1.6 nodeD=1.4 paraD1=1.4 paraD2=1.6 deltax=200 paralength2=10 nl=30}
	if (fiberD==5.7) {g=0.605 axonD=3.4 nodeD=1.9 paraD1=1.9 paraD2=3.4 deltax=500 paralength2=35 nl=80}
	if (fiberD==7.3) {g=0.630 axonD=4.6 nodeD=2.4 paraD1=2.4 paraD2=4.6 deltax=750 paralength2=38 nl=100}
	if (fiberD==8.7) {g=0.661 axonD=5.8 nodeD=2.8 paraD1=2.8 paraD2=5.8 deltax=1000 paralength2=40 nl=110}
	if (fiberD==10.0) {g=0.690 axonD=6.9 nodeD=3.3 paraD1=3.3 paraD2=6.9 deltax=1150 paralength2=46 nl=120}
	if (fiberD==11.5) {g=0.700 axonD=8.1 nodeD=3.7 paraD1=3.7 paraD2=8.1 deltax=1250 paralength2=50 nl=130}
	if (fiberD==12.8) {g=0.719 axonD=9.2 nodeD=4.2 paraD1=4.2 paraD2=9.2 deltax=1350 paralength2=54 nl=135}
	if (fiberD==14.0) {g=0.739 axonD=10.4 nodeD=4.7 paraD1=4.7 paraD2=10.4 deltax=1400 paralength2=56 nl=140}
	if (fiberD==15.0) {g=0.767 axonD=11.5 nodeD=5.0 paraD1=5.0 paraD2=11.5 deltax=1450 paralength2=58 nl=145}
	if (fiberD==16.0) {g=0.791 axonD=12.7 nodeD=5.5 paraD1=5.5 paraD2=12.7 deltax=1500 paralength2=60 nl=150}
	Rpn0=(rhoa*.01)/(PI*((((nodeD/2)+space_p1)^2)-((nodeD/2)^2)))
	Rpn1=(rhoa*.01)/(PI*((((paraD1/2)+space_p1)^2)-((paraD1/2)^2)))
	Rpn2=(rhoa*.01)/(PI*((((paraD2/2)+space_p2)^2)-((paraD2/2)^2)))
	Rpx=(rhoa*.01)/(PI*((((axonD/2)+space_i)^2)-((axonD/2)^2)))
	interlength=(deltax-nodelength-(2*paralength1)-(2*paralength2))/6
	//print interlength	
	}
dependent_var()

// ********************************************** Create model ****************************************************
create node[axonnodes], MYSA[paranodes1], FLUT[paranodes2], STIN[axoninter]
//access node[0]	//APD
objref x_axon
x_axon = new Vector(axontotal,0)

objref checknode_values
checknode_values = new Vector(Nchecknodes,0)

// s is a pointer to all the elements of the neuron (i.e. dendrites, soma, axon)
// s[i] = new SectionRef()
//  - keeps a pointer reference to a given section
objref s[axontotal]

proc initialize(){
	for i=0,axonnodes-1 {
		node[i]{					
			s[i*11]	= new SectionRef()
			//print i*11
			nseg	= 1
			diam	= nodeD
			L		= nodelength
			Ra		= rhoa/10000
			cm		= 2
			
			// Passive end nodes
			if(i == 0 || i == axonnodes-1){
				insert pas
				g_pas = 0.0001
				e_pas = -70
				insert extracellular
				xg      = mygm/(nl*2) 
				xc      = mycm/(nl*2) 
			} else {
				insert axnode
				insert extracellular xraxial=Rpn0 
				xg=1e10 // short circuit
				xc=0    // short circuit
			}
			
			}
		}
	for i=0, paranodes1-1 {
		MYSA[i]{
			if ((i%2) == 0) {
				s[((i/2)*11)+1] = new SectionRef()
				//print ((i/2)*11)+1
			} else {
				s[(((i+1)/2)*11)-1] = new SectionRef()
				//print (((i+1)/2)*11)-1
			}
			nseg	= 1
			diam	= fiberD
			L		= paralength1
			Ra		= rhoa*(1/(paraD1/fiberD)^2)/10000
			cm		= 2*paraD1/fiberD
			insert pas
			g_pas	= 0.001*paraD1/fiberD		
			e_pas	= -80//v_init
			insert extracellular xraxial=Rpn1 xg=mygm/(nl*2) xc=mycm/(nl*2)
			}
	}
	for i=0, paranodes2-1 {
		FLUT[i]{
			if ((i%2) == 0) {
				s[((i/2)*11)+2] = new SectionRef()
				//print ((i/2)*11)+2
			} else {
				s[(((i+1)/2)*11)-2] = new SectionRef()
				//print (((i+1)/2)*11)-2
			}
			nseg	= 1
			diam	= fiberD
			L		= paralength2
			Ra		= rhoa*(1/(paraD2/fiberD)^2)/10000
			cm		= 2*paraD2/fiberD
			insert pas
			g_pas	= 0.0001*paraD2/fiberD		
			e_pas	= -80//v_init
			insert extracellular xraxial=Rpn2 xg=mygm/(nl*2) xc=mycm/(nl*2)
			}
	}
	for i=0, axoninter-1 {
		STIN[i]{
			if ((i%6) == 5) {
				s[(((i+1)/6)*11)-3] = new SectionRef()
				//print (((i+1)/6)*11)-3
			} else if ((i%6) == 4) {
				s[(((i+2)/6)*11)-4] = new SectionRef()
				//print (((i+2)/6)*11)-4
			} else if ((i%6) == 3) {
				s[(((i+3)/6)*11)-5] = new SectionRef()
				//print (((i+3)/6)*11)-5
			} else if ((i%6) == 2) {
				s[(((i+4)/6)*11)-6] = new SectionRef()
				//print (((i+4)/6)*11)-6
			} else if ((i%6) == 1) {
				s[(((i+5)/6)*11)-7] = new SectionRef()
				//print (((i+5)/6)*11)-7
			} else if ((i%6) == 0) {
				s[(((i+6)/6)*11)-8] = new SectionRef()
				//print (((i+6)/6)*11)-8
			} 
			nseg	= 1
			diam	= fiberD
			L		= interlength
			Ra		= rhoa*(1/(axonD/fiberD)^2)/10000
			cm		= 2*axonD/fiberD
			insert pas
			g_pas	= 0.0001*axonD/fiberD
			e_pas	= -80//v_init
			insert extracellular xraxial=Rpx xg=mygm/(nl*2) xc=mycm/(nl*2)
			}
	}
	for i=0, axonnodes-2 {
		connect MYSA[2*i](0), node[i](1)
		connect FLUT[2*i](0), MYSA[2*i](1)
		connect STIN[6*i](0), FLUT[2*i](1)
		connect STIN[6*i+1](0), STIN[6*i](1)
		connect STIN[6*i+2](0), STIN[6*i+1](1)
		connect STIN[6*i+3](0), STIN[6*i+2](1)
		connect STIN[6*i+4](0), STIN[6*i+3](1)	
		connect STIN[6*i+5](0), STIN[6*i+4](1)	
		connect FLUT[2*i+1](0), STIN[6*i+5](1)
		connect MYSA[2*i+1](0), FLUT[2*i+1](1)
		connect node[i+1](0), MYSA[2*i+1](1)	
		}
	
	/*
	// set up vector with x coordinates of centres of each node
	x_axon.x[0] = 0//nodelength/2
	for i = 1, axonnodes-1 {
		ind = (i-1) * 11
		x_axon.x[ind+1] = x_axon.x[ind] + nodelength/2 + paralength1/2
		x_axon.x[ind+2] = x_axon.x[ind+1] + paralength1/2 + paralength2/2
		x_axon.x[ind+3] = x_axon.x[ind+2] + paralength2/2 + interlength/2
		x_axon.x[ind+4] = x_axon.x[ind+3] + interlength
		x_axon.x[ind+5] = x_axon.x[ind+4] + interlength
		x_axon.x[ind+6] = x_axon.x[ind+5] + interlength
		x_axon.x[ind+7] = x_axon.x[ind+6] + interlength
		x_axon.x[ind+8] = x_axon.x[ind+7] + interlength
		x_axon.x[ind+9] = x_axon.x[ind+8] + interlength/2 + paralength2/2
		x_axon.x[ind+10] = x_axon.x[ind+9] + paralength2/2 + paralength1/2
		x_axon.x[ind+11] = x_axon.x[ind+10] + paralength1/2 + nodelength/2
	}
	
	// convert from um to mm
	x_axon = x_axon.mul(1/1000)
	*/
	
	// Start counting nodes of Ranvier at #0
	
	// node where intracellular test stim is placed
	// 6mm from left end of 60mm fiber
	x_intracellstim_ind = int(6000/deltax)			// [unitless]
	
	// node where the extracellular stimulation (Ve) is recorded 
	// Halfway along the fiber
	checknode1			= int((axonnodes-1)/2)
	
	/*
	// nodes where Vm(t) is recorded [unitless]
	// 15mm from end of 60mm fiber; halfway along fiber; 45mm from end
	axon_length = (axonnodes-1)*deltax				// length of axon [um]
	checknode_values.x[0] = int(15000/deltax)
	checknode_values.x[1] = int(0.5*(axonnodes-1))
	checknode_values.x[2] = int((axon_length-15000)/deltax)
	checknode_values.x[3] = int((axon_length-5000)/deltax)
	*/
	
	// obtain x coordinate of extracellular VBLOC electrode (mm)
	//x_vblocstim = x_axon.x[x_vblocstim_ind*11]
}
initialize()

// **************************************** Read in extracellular potentials *********************************************
//prepare variables to read in the extracellular potentials
objref Ve_file, Ve_data, Ve_data_scaled, I_file, Iapplied
strdef fname

proc Ve_read() {local mymodel, myfasc, myaxon, myvblocamp
	mymodel = $1
	myfasc = $2
	myaxon = $3
	myvblocamp = $4

	sprint(fname, "Data/Model%d/Model%d_FiberD5-7_Fascicle%d_Axon%d.dat", mymodel, mymodel, myfasc, myaxon)
	print fname
	Ve_file = new File()
	Ve_file.ropen(fname)
	Ve_data = new Vector(axonnodes)
	Ve_data.scanf(Ve_file)
	Ve_file.close()
	// convert to mV (required for e_extracellular)
	Ve_data = Ve_data.mul(1000)
	
	sprint(fname, "Data/Model%d/Model%d_TotalAppliedCurrent.dat", mymodel, mymodel)
	I_file = new File()
	I_file.ropen(fname)
	Iapplied = new Vector(1)
	Iapplied.scanf(I_file)
	I_file.close()
	
	// Scale Ve data for desired stimulation amplitude
	Ve_data_scaled = new Vector(axonnodes)
	Ve_data_scaled = Ve_data.mul(myvblocamp/Iapplied.x[0])
}

// ******************************************** Recording & stimulation **************************************************
// recording vectors
objref Ve_vblocstim
Ve_vblocstim = new Vector(n_tsteps,0)

objref test_stim
test_stim = new Vector(n_tsteps,0)

objref volmem[axonnodes]
for i=0, axonnodes-1 {
	volmem[i] = new Vector(n_tsteps,0)
	volmem[i].record(&s[11*i].sec.v(0.5),dt)
}

/*
objref volmem[Nchecknodes]
for i=0, Nchecknodes - 1 {
	volmem[i] = new Vector(n_tsteps,0)
	volmem[i].record(&s[11*checknode_values.x[i]].sec.v(0.5),dt)
}
//volmem[K/11].x[J] = s[K].sec.v(0.5)
*/

objref m_rec[axonnodes], mp_rec[axonnodes], s_rec[axonnodes], h_rec[axonnodes]
m_rec[0] = new Vector(n_tsteps,0)
mp_rec[0] = new Vector(n_tsteps,0)
h_rec[0] = new Vector(n_tsteps,0)
s_rec[0] = new Vector(n_tsteps,0)

m_rec[axonnodes-1] = new Vector(n_tsteps,0)
mp_rec[axonnodes-1] = new Vector(n_tsteps,0)
h_rec[axonnodes-1] = new Vector(n_tsteps,0)
s_rec[axonnodes-1] = new Vector(n_tsteps,0)

//for i=0, axonnodes-1 {
for i=1, axonnodes-2 {
	m_rec[i] = new Vector(n_tsteps,0)
	m_rec[i].record(&s[11*i].sec.m_axnode(0.5),dt)
	
	mp_rec[i] = new Vector(n_tsteps,0)
	mp_rec[i].record(&s[11*i].sec.mp_axnode(0.5),dt)
	
	h_rec[i] = new Vector(n_tsteps,0)
	h_rec[i].record(&s[11*i].sec.h_axnode(0.5),dt)
	
	s_rec[i] = new Vector(n_tsteps,0)
	s_rec[i].record(&s[11*i].sec.s_axnode(0.5),dt)
}

//intracellular and extracellular stimuli//
objectvar stim, vblocstim1, vblocstim2
create myelec1, myelec2

proc stimul() {local dtsav, myvblocfreq, myvblocpw
	myvblocfreq = $1
	myvblocpw = (1000/myvblocfreq)/2 // 1000-> conversion from seconds to milliseconds

	node[x_intracellstim_ind]{
		//print x_intracellstim_ind
		stim=new trainIClamp()
		stim.loc(.5)
		stim.del=delay
		stim.PW=pw
		stim.train = traindur1
		stim.freq=ifreq
		stim.amp=istim
	}
		
	myelec1 {
		vblocstim1=new trainIClamp()
		vblocstim1.loc(.5)
		vblocstim1.del		=vblocdelay1
		vblocstim1.PW		=myvblocpw//vblocpw1
		vblocstim1.train	=vblocdur1
		vblocstim1.freq		=myvblocfreq//vblocfreq1
		vblocstim1.amp		=vblocistim1
	}
	
	myelec2 {
		vblocstim2=new trainIClamp()
		vblocstim2.loc(.5)
		vblocstim2.del		=vblocdelay1 + myvblocpw//vblocdelay2
		vblocstim2.PW		=myvblocpw//vblocpw2
		vblocstim2.train	=tstop - vblocstim2.del//vblocdur2
		vblocstim2.freq		=myvblocfreq//vblocfreq2
		vblocstim2.amp		=vblocistim2
	}
	
	finitialize(v_init)
	for K = 0, axontotal-1 {
		s[K].sec.e_extracellular(0.5) = 0
	}
	
	//fcurrent()
	// Allow time for model to read steady-state using large dt before actually starting stimulation
	t = t_initSS			// start before t=0
	dtsav = dt
	dt = dt_initSS			// large dt
	while (t <= -dt) {
		fadvance()
	}
	dt = dtsav
	t = 0
	fcurrent()
	frecord_init()
	
	for J=0, n_tsteps-1 {
		for K = 0, axontotal-1 {
		
			tmp = vblocstim1.i + vblocstim2.i
			if (tmp == 0) {
				s[K].sec.e_extracellular(0.5) = 0
			} else if (tmp > 0) {
				s[K].sec.e_extracellular(0.5) = Ve_data_scaled.x[K]
			} else if (tmp < 0) {
				s[K].sec.e_extracellular(0.5) = -Ve_data_scaled.x[K]
			} else {
				print "ERROR: Problem with stimulus"
				execerror("ERROR: Problem with stimulus")
				stop
			}
			/*
			if ((K%11) == 0) {
				// record membrane potential at current node and current time step
				volmem[K/11].x[J] = s[K].sec.v(0.5)
				//volmem[K/11].x[J] = node[K/11].v(0.5)
			}
			*/
		}
		Ve_vblocstim.x[J] = s[checknode1*11].sec.e_extracellular(0.5)
		test_stim.x[J] = stim.i
		fadvance()
	}
}

// ********************************************** Save data ****************************************************
objref f1

proc save_data() {local mymodel, myfasc, myaxon, myvblocamp, myvblocfreq
	mymodel = $1
	myfasc = $2
	myaxon = $3
	myvblocamp = $4
	myvblocfreq = $5
	
	// Save inputs (istim and Ve)
	sprint(fname, "Data/Model%d/%dHz/inputs_fasc%d_Vblocamp%f.dat", mymodel, myvblocfreq, myfasc, myvblocamp)
	f1 = new File()
	f1.wopen(fname)
	f1.printf("Time(ms) test_stim(nA) Ve_node%d(mV) ", checknode1)
	f1.printf("\n")
	
	for J = 0, n_tsteps-1 {
		f1.printf("%f ", J*dt)
		f1.printf("%f ", test_stim.x[J])
		f1.printf("%f ", Ve_vblocstim.x[J])
		f1.printf("\n")
	}
	
	f1.close()
	

	// Save Vm(t) for all nodes
	sprint(fname, "Data/Model%d/%dHz/Vm_fasc%d_Vblocamp%f.dat", mymodel, myvblocfreq, myfasc, myvblocamp)
	f1 = new File()
	f1.wopen(fname)
	f1.printf("Time(ms) ")
	for i = 0, axonnodes-1 {
		f1.printf("Vm_node%d(mV) ", i+1)
	}
	f1.printf("\n")
		
	for J = 0, n_tsteps-1 {
		f1.printf("%f ", J*dt)
		for i = 0, axonnodes-1 {
			f1.printf("%f ", volmem[i].x[J])
		}
		f1.printf("\n")
	}
	f1.close()
	
	// Save m_axnode(t) for all nodes
	sprint(fname, "Data/Model%d/%dHz/m_fasc%d_Vblocamp%f.dat", mymodel, myvblocfreq, myfasc, myvblocamp)
	f1 = new File()
	f1.wopen(fname)
	f1.printf("Time(ms) ")
	for i = 0, axonnodes-1 {
		f1.printf("m_node%d ", i+1)
	}
	f1.printf("\n")
		
	for J = 0, n_tsteps-1 {
		f1.printf("%f ", J*dt)
		for i = 0, axonnodes-1 {
			f1.printf("%f ", m_rec[i].x[J])
		}
		f1.printf("\n")
	}
	f1.close()
	
	// Save mp_axnode(t) for all nodes
	sprint(fname, "Data/Model%d/%dHz/mp_fasc%d_Vblocamp%f.dat", mymodel, myvblocfreq, myfasc, myvblocamp)
	f1 = new File()
	f1.wopen(fname)
	f1.printf("Time(ms) ")
	for i = 0, axonnodes-1 {
		f1.printf("mp_node%d ", i+1)
	}
	f1.printf("\n")
		
	for J = 0, n_tsteps-1 {
		f1.printf("%f ", J*dt)
		for i = 0, axonnodes-1 {
			f1.printf("%f ", mp_rec[i].x[J])
		}
		f1.printf("\n")
	}
	f1.close()
	
	// Save h_axnode(t) for all nodes
	sprint(fname, "Data/Model%d/%dHz/h_fasc%d_Vblocamp%f.dat", mymodel, myvblocfreq, myfasc, myvblocamp)
	f1 = new File()
	f1.wopen(fname)
	f1.printf("Time(ms) ")
	for i = 0, axonnodes-1 {
		f1.printf("h_node%d ", i+1)
	}
	f1.printf("\n")
		
	for J = 0, n_tsteps-1 {
		f1.printf("%f ", J*dt)
		for i = 0, axonnodes-1 {
			f1.printf("%f ", h_rec[i].x[J])
		}
		f1.printf("\n")
	}
	f1.close()
	
	// Save s_axnode(t) for all nodes
	sprint(fname, "Data/Model%d/%dHz/s_fasc%d_Vblocamp%f.dat", mymodel, myvblocfreq, myfasc, myvblocamp)
	f1 = new File()
	f1.wopen(fname)
	f1.printf("Time(ms) ")
	for i = 0, axonnodes-1 {
		f1.printf("s_node%d ", i+1)
	}
	f1.printf("\n")
		
	for J = 0, n_tsteps-1 {
		f1.printf("%f ", J*dt)
		for i = 0, axonnodes-1 {
			f1.printf("%f ", s_rec[i].x[J])
		}
		f1.printf("\n")
	}
	f1.close()
	
	print "finished saving ", fname
}

// ********************************************** Plot data ****************************************************

objref gstim1, gstim2, g1

proc plot_data() {
	gstim2 = new Graph()
	//xf = i_vblocstim.size - 1 // # of elements in volmem minus 1
	gstim2.size(0,tstop,-8,8)
	Ve_vblocstim.plot(gstim2,dt)

	g1 = new Graph()
	//xf = volmem[0].size - 1 // # of elements in volmem minus 1
	g1.size(0,tstop,-150,50)
	volmem[checknode1].plot(g1,dt)
}
//plot_data()

// ********************************* Call stimulation & saving procedures ****************************************
proc run_all(){local mymodel, myfasc, myaxon, myvblocamp, myvblocfreq
	mymodel = $1
	myfasc = $2
	myaxon = $3
	myvblocamp = $4
	myvblocfreq = $5
	
	Ve_read(mymodel, myfasc, myaxon, myvblocamp)
	stimul(myvblocfreq)
	save_data(mymodel, myfasc, myaxon, myvblocamp, myvblocfreq)
}

// *********************************************** Batch run *****************************************************

trun = startsw()

// Start execute loop on the workers
{pc.runworker()}
// Code beyond this point (til pc.done()) is only executed by the master.
// The master must now post jobs to the bulletin board.

proc batchrun() { local model_ind, fasc_ind, axon_ind, vblocamp_ind, vblocfreq_ind

	for vblocfreq_ind = 0, Nvblocfreq-1 {
		for model_ind = 0, Nmodel-1 {
			for fasc_ind = 0, Nfasc-1 {
				for axon_ind = 0, Naxon-1 {
					for vblocamp_ind = 0, Nvblocamp-1 {
						pc.submit("run_all",model_values.x[model_ind],fasc_values.x[fasc_ind],axon_values.x[axon_ind],vblocamp_values.x[vblocamp_ind],vblocfreq_values.x[vblocfreq_ind])
					}
				}
			}
		}
	}

	// If a result is ready, get it. If not, pick a job to do.
	while (pc.working()) {
	}
	
}
batchrun()


// Save the runtime to a file
objref f_runtime
f_runtime = new File()
f_runtime.wopen("Data/run_time.dat")
f_runtime.printf("Run time: %f seconds", startsw()-trun)
f_runtime.close()
//print "Run time: ", startsw()-trun, "seconds"

{pc.done()}

quit()
