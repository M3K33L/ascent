/*
The copyrights of this software are owned by Duke University.
Please refer to the LICENSE.txt and README.txt files for licensing instructions.
The source code can be found on the following GitHub repository: https://github.com/wmglab-duke/ascent
*/

/*
Author: Nikki Pelot
Date created: January 29, 2016

Description:
- Create unmyelinated axon using MRG ion channels.
- Include section ref's to keep format consistent with CreateAxon_Myel.hoc (e.g. to apply extracellular potentials).

Variables that must be defined in wrapper/params file:
- axonnodes
- fiberd
- deltaz
- passive_end_nodes
*/

create node[1]
objref s[1]

proc CreateAxon_Unmyel() {
	//  Electrical parameters
	rhoa	= 0.7e6 						// [ohm-um]
	mycm	= 0.1 							// lamella membrane; [uF/cm2]
	mygm	= 0.001 						// lamella membrane; [S/cm2]
	rhoe	= 1000  						// resistivity of extracellular medium; [ohm-cm]

	// Geometrical parameters
	axontotal 	= axonnodes 				// Total number of segments = total number of sections
	nl  		= 25						// For passive end nodes
	
	// Create axon
	create node[axonnodes]

	// s is a pointer to all the elements of the neuron (i.e. dendrites, soma, axon)
	// s[i] = new SectionRef()
	//  - keeps a pointer reference to a given section
	objref s[axontotal]
	
	for i=0,axonnodes-1 {
		node[i]{
			s[i]	= new SectionRef()
			nseg	= 1
			diam	= fiberd
			L		= deltaz
			Ra		= rhoa/10000
			cm		= 2
			
			// Passive end nodes
			if ((i == 0 || i == axonnodes-1) && (passive_end_nodes == 1)) {
				//print "i for passive: ", i
				insert pas
				g_pas = 0.0001
				e_pas = -70
				insert extracellular
				xg      = mygm/(nl*2) 
				xc      = mycm/(nl*2) 
			} else {
				insert axnode_unmyel
				insert extracellular
				xg=1e10 // short circuit
				xc=0    // short circuit
			}
			
		}
	}

	for i=0, axonnodes-2 {
		connect node[i](0), node[i+1](1)
	}
}
